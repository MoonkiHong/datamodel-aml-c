###############################################################################
# Copyright 2018 Samsung Electronics All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
###############################################################################

##
# AML DataModel Unit Test build script
##

import os
from build_common.tools.RunTest import run_test

Import('env')

gtest_env = SConscript('#extlibs/gtest/SConscript')
lib_env = gtest_env.Clone()

if lib_env.get('RELEASE'):
    lib_env.AppendUnique(CCFLAGS=['-Os'])
else:
    lib_env.AppendUnique(CCFLAGS=['-g'])
    lib_env.AppendUnique(CPPDEFINES=['DEBUG_LOG'])


caml_test_env = lib_env.Clone()
target_os = caml_test_env.get('TARGET_OS')
target_arch = caml_test_env.get('TARGET_ARCH')
disable_protobuf = caml_test_env.get('DISABLE_PROTOBUF')

######################################################################
# Build flags
######################################################################

build_mode = 'release'
if not caml_test_env.get('RELEASE'):
    build_mode = 'debug'

caml_test_env.PrependUnique(LIBS=['aml'], LIBPATH=[os.path.join('./../dependencies/datamodel-aml-cpp/out/', target_os, target_arch, build_mode)])

#caml_test_env.AppendUnique(LIBPATH=[lib_env.get('BUILD_DIR')])
caml_test_env.AppendUnique(LIBS=['caml'])

if not disable_protobuf:
    caml_test_env.AppendUnique(LIBS=['protobuf'])

if target_os not in ['windows']:
    caml_test_env.AppendUnique(
        CXXFLAGS=['-O2', '-g', '-Wall', '-fmessage-length=0', '-I/usr/local/include'])

if not caml_test_env.get('RELEASE'):
    caml_test_env.PrependUnique(LIBS=['gcov'])
    caml_test_env.AppendUnique(CXXFLAGS=['--coverage'])

caml_test_env.AppendUnique(CPPPATH=[
    '../include',
    '../src',
    '.'
])

if not disable_protobuf:
    caml_test_env.AppendUnique(CPPPATH=['../protobuf'])
else:
    caml_test_env.AppendUnique(CPPDEFINES = ['_DISABLE_PROTOBUF_'])

######################################################################
# Build Test
######################################################################

caml_rep_test_src = [
    'camlrepresentationtest.cpp',
    'camlinterfacetest.cpp'
]

caml_rep_test = caml_test_env.Program('caml_rep_test', caml_rep_test_src)

Alias("caml_rep_test", caml_rep_test)
caml_test_env.AppendTarget('caml_rep_test')

Command("TEST_Data.aml", File("TEST_Data.aml").srcnode(), Copy("$TARGET", "$SOURCE"))
Command("TEST_DataBinary", File("TEST_DataBinary").srcnode(), Copy("$TARGET", "$SOURCE"))
Command("TEST_DataModel.aml", File("TEST_DataModel.aml").srcnode(), Copy("$TARGET", "$SOURCE"))
Command("TEST_DataModel_Invalid_NoCAEX.aml", File("TEST_DataModel_Invalid_NoCAEX.aml").srcnode(), Copy("$TARGET", "$SOURCE"))
Command("TEST_DataModel_Invalid_NoSUCL.aml", File("TEST_DataModel_Invalid_NoSUCL.aml").srcnode(), Copy("$TARGET", "$SOURCE"))

if env.get('TEST') == '1':
	#Command("copy_tmp_file", File("TEST_DataModel.aml").srcnode(), Copy("./", "$SOURCE"))
	run_test(caml_test_env, '', 'unittests/caml_rep_test', caml_rep_test)
